(* /////////////////////// IDENTIFIER ////////////////////// *)

identifier = identifier_start, { identifier_part } ;

identifier_start = letter | "_" ;

identifier_part = letter | digit | "_" ;

letter = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M"
       | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
       | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m"
       | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" ;

digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

(* /////////////////////// INT ////////////////////// *)
integer = signed_integer | unsigned_integer ;

sign = "+" | "-" ;

signed_integer = [sign], unsigned_integer ;

unsigned_integer = integer_literal ;

integer_literal = decimal_literal | octal_literal | hexadecimal_literal | binary_literal ;

decimal_literal = "0" | non_zero_digit, { ["'"], digit } ; 

octal_literal = "0", { octal_digit } ;

hexadecimal_literal = hexadecimal_prefix, hexadecimal_digit, { hexadecimal_digit } ;

binary_literal = binary_prefix, binary_digit, { binary_digit } ;

hexadecimal_prefix = "0x" | "0X" ;

binary_prefix = "0b" | "0B" ;

non_zero_digit = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

octal_digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" ;

hexadecimal_digit = digit | "A" | "B" | "C" | "D" | "E" | "F"
                          | "a" | "b" | "c" | "d" | "e" | "f" ;

binary_digit = "0" | "1" ;

(* /////////////////////// FLOAT ////////////////////// *)
floating_literal = decimal_floating_literal | hexadecimal_floating_literal ;

decimal_floating_literal = ( fractional_constant, [ exponent_part ], [ floating_suffix ] )
                         | ( digit_sequence, exponent_part, [ floating_suffix ] ) ;

fractional_constant = [ digit_sequence ], ".", digit_sequence    (* "123." и ".456" валидны в C++ *)
                    | digit_sequence, "." ;                      (* "123." = 123.0, ".456" = 0.456 *)
                                                                 (* узнал об этом когда писал md *)

exponent_part = ( "e" | "E" ), [ "+" | "-" ], digit_sequence ;

digit_sequence = digit, { digit } ;

floating_suffix = "f" | "l" | "F" | "L" ;

hexadecimal_floating_literal = hexadecimal_prefix,
                               ( hexadecimal_fractional_constant | hexadecimal_digit_sequence ),
                               binary_exponent_part,
                               [ floating_suffix ] ;

hexadecimal_fractional_constant = [ hexadecimal_digit_sequence ], ".", hexadecimal_digit_sequence
                                | hexadecimal_digit_sequence, "." ;

binary_exponent_part = ( "p" | "P" ), [ "+" | "-" ], digit_sequence ;

hexadecimal_digit = digit | "A" | "B" | "C" | "D" | "E" | "F"
                          | "a" | "b" | "c" | "d" | "e" | "f" ;

hexadecimal_digit_sequence = hexadecimal_digit, { hexadecimal_digit } ;

(* /////////////////////// STRING ////////////////////// *)
string_literal = [ encoding_prefix ], ( raw_string_literal | ordinary_string_literal ) ;

encoding_prefix = "u8" | "u" | "U" | "L" ;

ordinary_string_literal = '"', { ordinary_char }, '"' ;

ordinary_char = any_char_except_special | escape_sequence ;

any_char_except_special = ? any character except '"', '\', newline ? ;

raw_string_literal = '"', raw_delimiter, '(', { raw_char }, ')', raw_delimiter, '"' ;

raw_delimiter = { raw_delimiter_char } ;

raw_delimiter_char = letter | digit | "_" ;

raw_char = ? any character except sequence ')', raw_delimiter, '"' ? ;

escape_sequence = 
    "\", ( '"' | "'" | "\" | "?" | "a" | "b" | "f" | "n" | "r" | "t" | "v"
    | octal_escape | hexadecimal_escape | universal_character_name ) ;

octal_escape = "\", octal_digit, [ octal_digit, [ octal_digit ] ] ;

hexadecimal_escape = "\x", hexadecimal_digit, { hexadecimal_digit } ;

universal_character_name = 
    "\u", hexadecimal_digit, hexadecimal_digit, hexadecimal_digit, hexadecimal_digit
    | "\U", hexadecimal_digit, hexadecimal_digit, hexadecimal_digit, hexadecimal_digit,
      hexadecimal_digit, hexadecimal_digit, hexadecimal_digit, hexadecimal_digit ;